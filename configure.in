dnl#
dnl# "$Id: configure.in,v 1.4 1999/04/21 22:50:18 mike Exp $"
dnl#
dnl#   Configuration script for the Common UNIX Printing System (CUPS).
dnl#
dnl#   Copyright 1997-1999 by Easy Software Products, all rights reserved.
dnl#
dnl#   These coded instructions, statements, and computer programs are the
dnl#   property of Easy Software Products and are protected by Federal
dnl#   copyright law.  Distribution and use rights are outlined in the file
dnl#   "LICENSE.txt" which should have been included with this file.  If this
dnl#   file is missing or damaged please contact Easy Software Products
dnl#   at:
dnl#
dnl#       Attn: CUPS Licensing Information
dnl#       Easy Software Products
dnl#       44145 Airport View Drive, Suite 204
dnl#       Hollywood, Maryland 20636-3111 USA
dnl#
dnl#       Voice: (301) 373-9603
dnl#       EMail: cups-info@cups.org
dnl#         WWW: http://www.cups.org
dnl#

AC_INIT(cups/cups.h)
AC_CONFIG_HEADER(config.h)
AC_PREFIX_DEFAULT(/usr)

dnl# Clear default debugging options and set normal optimization by
dnl# default unless the user asks for debugging specifically.

CFLAGS="${CFLAGS:=}"
CXXFLAGS="${CXXFLAGS:=}"
OPTIM="-O"
AC_SUBST(OPTIM)

AC_ARG_ENABLE(debug, [  --enable-debug          turn on debugging [default=no]],
if eval "test x$enable_debug = xyes"; then
	OPTIM="-g"
fi)

dnl# Checks for programs...
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(FLUID,fluid)
AC_PATH_PROG(GROFF,groff)
AC_PATH_PROG(HTMLDOC,htmldoc)

dnl# Architecture checks...
AC_C_BIGENDIAN

dnl# Check for libraries...
AC_CHECK_LIB(sec,getspent)

AC_PATH_XTRA
if test "$X_PRE_LIBS" != ""; then
	echo "Ignoring libraries \"$X_PRE_LIBS\" requested by configure."
fi
if test "$X_LIBS" = " -L/usr/lib"; then
	echo "Ignoring X library directory \"$X_LIBS\" requested by configure."
	X_LIBS=""
fi
GUILIBS=""
AC_SUBST(GUILIBS)
AC_CHECK_HEADER(FL/Fl.H,GUILIBS="$X_LIBS -lfltk -lX11 -lXext"
AC_DEFINE(HAVE_LIBFLTK))

IMGLIBS=""
AC_SUBST(IMGLIBS)
AC_CHECK_LIB(jpeg,jpeg_destroy_decompress,AC_DEFINE(HAVE_LIBJPEG)
IMGLIBS="$IMGLIBS -ljpeg")
AC_CHECK_LIB(png,png_init_io,AC_DEFINE(HAVE_LIBPNG)
IMGLIBS="$IMGLIBS -lpng")
AC_CHECK_LIB(tiff,TIFFFdOpen,AC_DEFINE(HAVE_LIBTIFF)
IMGLIBS="$IMGLIBS -ltiff")
AC_CHECK_LIB(z,deflate,AC_DEFINE(HAVE_LIBZ)
IMGLIBS="$IMGLIBS -lz")

dnl# Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_CHECK_HEADER(shadow.h,AC_DEFINE(HAVE_SHADOW_H))

dnl# Checks for string functions.
AC_CHECK_FUNCS(strdup)
AC_CHECK_FUNCS(strcasecmp)
AC_CHECK_FUNCS(strncasecmp)

dnl# Fix "prefix" variable if it hasn't been specified...
if test "$prefix" = "NONE"; then
	prefix="/usr"
fi

dnl# SERVERDIR needs special attention for the default location...
if test "$prefix" = "/usr"; then
	AC_DEFINE(SERVERDIR, "/var/cups")
else
	AC_DEFINE_UNQUOTED(SERVERDIR, "$prefix/var/cups")
fi

dnl# Set the LOCALE directory...
AC_DEFINE_UNQUOTED(LOCALEDIR, "$prefix/lib/locale")

AC_OUTPUT(Makedefs)

dnl#
dnl# End of "$Id: configure.in,v 1.4 1999/04/21 22:50:18 mike Exp $".
dnl#
