dnl
dnl "$Id: configure.in,v 1.16 1999/06/21 15:27:41 mike Exp $"
dnl
dnl   Configuration script for the Common UNIX Printing System (CUPS).
dnl
dnl   Copyright 1997-1999 by Easy Software Products, all rights reserved.
dnl
dnl   These coded instructions, statements, and computer programs are the
dnl   property of Easy Software Products and are protected by Federal
dnl   copyright law.  Distribution and use rights are outlined in the file
dnl   "LICENSE.txt" which should have been included with this file.  If this
dnl   file is missing or damaged please contact Easy Software Products
dnl   at:
dnl
dnl       Attn: CUPS Licensing Information
dnl       Easy Software Products
dnl       44141 Airport View Drive, Suite 204
dnl       Hollywood, Maryland 20636-3111 USA
dnl
dnl       Voice: (301) 373-9603
dnl       EMail: cups-info@cups.org
dnl         WWW: http://www.cups.org
dnl

AC_INIT(cups/cups.h)
AC_CONFIG_HEADER(config.h)
AC_PREFIX_DEFAULT(/usr)

dnl Clear default debugging options and set normal optimization by
dnl default unless the user asks for debugging specifically.

dnl CFLAGS="${CFLAGS:=}"
dnl CXXFLAGS="${CXXFLAGS:=}"
dnl OPTIM="-O"
dnl AC_SUBST(OPTIM)

dnl AC_ARG_ENABLE(debug, [  --enable-debug          turn on debugging [default=no]],
dnl if eval "test x$enable_debug = xyes"; then
dnl 	OPTIM="-g"
dnl fi)

dnl Clear default debugging options and set debugging by
dnl default unless the user asks for no debugging specifically.

CFLAGS="${CFLAGS:=}"
CXXFLAGS="${CXXFLAGS:=}"
OPTIM="-g"
AC_SUBST(OPTIM)

AC_ARG_ENABLE(debug, [  --disable-debug          turn off debugging [default=no]],
if eval "test x$disable_debug = xyes"; then
	OPTIM="-O"
fi)

dnl Checks for programs...
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_PROG_RANLIB
AC_PATH_PROG(AR,ar)
AC_PATH_PROG(CHMOD,chmod)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(NROFF,nroff)
if test "$NROFF" = ""; then
    AC_PATH_PROG(NROFF,groff)
    if test "$NROFF" = ""; then
        NROFF="echo"
    else
        NROFF="$NROFF -T ascii"
    fi
fi
AC_PATH_PROG(HTMLDOC,htmldoc)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG(PACK,pack)
if test "$PACK" = ""; then
    AC_PATH_PROG(PACK,gzip)
    if test "$PACK" = ""; then
        PACK="echo"
	CAT="dummy"
    else
        PACK="$PACK -fv9"
	CAT="gz"
    fi
else
    PACK="$PACK -f"
    CAT="z"
fi
AC_SUBST(CAT)
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(SED,sed)
AC_PATH_PROG(SMBCLIENT,smbclient)
if test "$SMBCLIENT" = ""; then
    echo "Looking for smbclient in standard locations..."
    AC_PATH_PROG(SMBCLIENT,smbclient,samba_not_detected,
	/usr/samba/bin:/usr/local/samba/bin:/usr/freeware/samba/bin:/opt/samba/bin)
fi

dnl Architecture checks...
AC_C_BIGENDIAN
uname=`uname`
if test "$uname" = "IRIX64"; then
	uname=IRIX
fi

dnl Check for libraries...
AC_CHECK_LIB(c,crypt,LIBS="$LIBS",AC_CHECK_LIB(crypt,crypt))
AC_CHECK_HEADER(crypt.h, AC_DEFINE(HAVE_CRYPT_H))
AC_CHECK_LIB(sec,getspent)
AC_CHECK_LIB(socket,socket,
if test "$uname" != "IRIX"; then
	LIBS="$LIBS -lsocket"
else
	echo "Not using -lsocket since you are running IRIX."
fi)
AC_CHECK_LIB(nsl,gethostbyaddr,
if test "$uname" != "IRIX"; then
	LIBS="$LIBS -lnsl"
else
	echo "Not using -lnsl since you are running IRIX."
fi)

IMGLIBS=""
AC_SUBST(IMGLIBS)
AC_CHECK_HEADER(jpeglib.h,
    AC_DEFINE(HAVE_LIBJPEG)
    IMGLIBS="$IMGLIBS -ljpeg")
AC_CHECK_HEADER(zlib.h,
    AC_DEFINE(HAVE_LIBZ)
    IMGLIBS="$IMGLIBS -lz")
AC_CHECK_HEADER(png.h,
    AC_DEFINE(HAVE_LIBPNG)
    IMGLIBS="$IMGLIBS -lpng")
AC_CHECK_HEADER(tiff.h,
    AC_DEFINE(HAVE_LIBTIFF)
    IMGLIBS="$IMGLIBS -ltiff")

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_CHECK_HEADER(shadow.h,AC_DEFINE(HAVE_SHADOW_H))

dnl Checks for string functions.
AC_CHECK_FUNCS(strdup)
AC_CHECK_FUNCS(strcasecmp)
AC_CHECK_FUNCS(strncasecmp)

dnl Checks for signal functions.
AC_CHECK_FUNCS(sigset)
AC_CHECK_FUNCS(sigaction)

dnl Checks for wait functions.
AC_CHECK_FUNCS(waitpid)
AC_CHECK_FUNCS(wait3)

dnl Fix "prefix" variable if it hasn't been specified...
if test "$prefix" = "NONE"; then
	prefix="/usr"
fi

dnl CUPS_SERVERROOT needs special attention for the default location...
if test "$prefix" = "/usr"; then
	CUPS_SERVERROOT="/var/cups"
else
	CUPS_SERVERROOT="$prefix/var/cups"
fi
AC_DEFINE_UNQUOTED(CUPS_SERVERROOT, "$CUPS_SERVERROOT")
AC_SUBST(CUPS_SERVERROOT)

dnl Set the CUPS_LOCALE directory...
CUPS_LOCALEDIR="$prefix/lib/locale"
AC_DEFINE_UNQUOTED(CUPS_LOCALEDIR, "$CUPS_LOCALEDIR")
AC_SUBST(CUPS_LOCALEDIR)

dnl Set the CUPS_DATAFILE directory...
CUPS_DATADIR="$prefix/share/cups"
AC_DEFINE_UNQUOTED(CUPS_DATADIR, "$CUPS_DATADIR")
AC_SUBST(CUPS_DATADIR)

AC_OUTPUT(Makedefs)

dnl
dnl End of "$Id: configure.in,v 1.16 1999/06/21 15:27:41 mike Exp $".
dnl
